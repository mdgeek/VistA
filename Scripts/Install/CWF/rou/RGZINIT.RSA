%RO Routine Output Utility
GT.M 24-FEB-2015 17:01:56
RGZINIT
RGZINIT ;RG/IND/DKM - Set up user access ;24-Feb-2015 17:00;SA
        ;=================================================================
        ;;1.0;CAREWEB FRAMEWORK;;30-Jan-2015 15:25
        ;=================================================================
        D INIT($ZCM)
        Q
AUTO    N IEN,NM,XX
        F IEN=0:0 S IEN=$O(^VA(200,IEN)) Q:'IEN  S NM=$P(^(IEN,0),U) D
        .F XX="NURSE","DOCTOR","SYSTEM","COORDINATOR","USER" I NM[XX D INIT(IEN) Q
        Q
INIT(NAM)       ;
        N DUZ
        S DUZ=+$S(NAM=+NAM:NAM,1:$$FIND1^DIC(200,,"X",NAM))
        I '$D(^VA(200,DUZ,0)) W "User not found: ",NAM,!!,"Exiting...",!!! Q
        W "Configuring user: ",NAM,!
        D ADDSK("RGCWF DESIGNER"),ADDSK("RGCWF COMPOSER"),ADDSK("RGCWPT PATIENT SELECT")
        D ADDXP("RGCWENCX CREATE VISIT"),ADDXP("RGCWENCX PROVIDER")
        Q
ADDSK(X)        W ?5,"Assigning security key: ",X,!
        S ^XUSEC(X,DUZ)=""
        Q
ADDXP(X)        N ERR
        W ?5,"Adding permission: ",X,!
        D ADD^XPAR("USR",X,1,1,.ERR)
        W:ERR ?7,"Error adding permission.",!
        Q
WWWINIT N FDA
        D FDASET("FHIR/*","+1,",.FDA)
        D FDASET("VISTA/*","+2,",.FDA)
        D:$D(FDA) UPDATE^DIE("E","FDA")
        Q
FDASET(URI,IENS,FDA)    ;
        I '$D(^%W(17.6001,"B","GET",URI)) D
        .S FDA(17.6001,IENS,.01)="GET"
        .S FDA(17.6001,IENS,1)=URI
        .S FDA(17.6001,IENS,2)="MSERVER^RGCWSER"
        Q



