DDG Supplemental routines
GT.M 24-FEB-2015 05:41:12
RGZDDG
RGZDDG ;RG/CBMI/DKM - Import dEWDrop globals;03-Mar-2015 13:58;DKM
 W "Imports dEWDrop globals.",!
 W "Entry point is DDG^RGZDDG",!!
 Q
 ; Command line entry point
DDG D EN($ZCM)
 Q
 ; Process global save (ZWR format), replacing root globals entirely
 ; (or partially for ^ZZ* globals) with new content.
EN(FILE) ;
 N X,Y,GBL,PRV,LN,$ET
 C FILE
 O FILE
 U FILE
 F X=1,2 R LN D MSG(LN)
 S PRV="",$ZT="ERROR^RGZDDG"
 F  R LN Q:'$L(LN)  D
 .S GBL=$P($P($P(LN,""""),"="),"(")
 .I PRV'=GBL D
 ..D PARTIAL
 ..S PRV=GBL
 ..K @GBL
 ..D MSG("   "_GBL)
 .S @LN
 D PARTIAL
 C FILE
 U 0
 F FILE=120.5 D REINDEX(FILE)
 W "Done!",!!
 Q
 ; ^ZZ* globals are partial copies
PARTIAL Q:$E(PRV,1,3)'="^ZZ"
 S X="^"_$E(PRV,4,99),Y=""
 F  S Y=$O(@PRV@(Y)) Q:'$L(Y)  D
 .D MSG("    "_$NA(@PRV@(Y))_" --> "_$NA(@X@(Y)))
 .K @X@(Y)
 .M @X@(Y)=@PRV@(Y)
 K @PRV
 Q
REINDEX(FILE) ;
 N DIK,DA
 W "Re-indexing file #",FILE,!
 S DIK=$$ROOT^DILFD(FILE)
 D IXALL^DIK
 Q
 ; Display message to console
MSG(X) U 0
 W X,!
 U FILE
 Q
 ; Error trap
ERROR C FILE
 U 0
 W $ZE,":  ",LN,!
 Q

XUSHSH
XUSHSH ;SF-ISC/STAFF - PASSWORD ENCRYPTION ;3/23/89  15:09 ; 4/14/05 1:22pm
 ;;8.0;KERNEL;;Jul 10, 1995
 ;; This is the public domain version of the VA Kernel.
 ;; Use this routine for your own encryption algorithm
 ;; Input in X
 ;; Output in X
 ;; Algorithm for VistA Office EHR encryption (BSL)
A ; 
 S X=$$EN(X)
 Q
EN(X) ; GENERIC HASHING ENCRYPTION -- USES ASCII ENCODING
 N %HASH S %HASH=""
 N %CHAR
 F %CHAR=1:1:$L(X) D
 . I %CHAR#2 S %HASH=$A(X,%CHAR)_%HASH
 . E  S %HASH=%HASH_$A(X,%CHAR)
 Q %HASH



